package drools.spring.rules

import rs.uns.ac.ftn.cdss.model.*;
import rs.uns.ac.ftn.cdss.model.util.*;
import java.util.*;

agenda-group "diseases-group"

rule "Prehlada"
	no-loop
	lock-on-active true
	salience ($sdc.getSalience()/$sdc.getMaxSymptomNum()*100)
	when
		$d : Disease( name == "Prehlada" )
		$mr : Record (disease == null , $symptoms: symptoms)
		$sdc : SalienceChecker( $s : salience>=4 ) from accumulate(
			$symptom : Symptom (
				name == "Curenje iz nosa" ||
				name == "Bol u grlu" ||
				name == "Glavobolja" ||
				name == "Kijanje" ||
				name == "Kasalj"
			) from $symptoms,		
            init(SalienceChecker $salCheck = new SalienceChecker(5);),
            action($salCheck.addSymptom();),
            result($salCheck)
			
		)
	then
		modify($mr) {setDisease($d);} 
		System.out.println("Prehlada: satisfied " + $sdc.getSalience() + " symptoms.");
end


rule "Groznica"
	no-loop
	lock-on-active true
	salience ($sdc.getSalience()/$sdc.getMaxSymptomNum()*100)
	when
		$d : Disease( name == "Groznica" )
		$mr : Record (disease == null , $symptoms: symptoms)
		$sdc : SalienceChecker( $s : salience>=4 ) from accumulate(
			$symptom : Symptom (
				name == "Curenje iz nosa" ||
				name == "Bol u grlu" ||
				name == "Glavobolja" ||
				name == "Kijanje" ||
				name == "Kasalj" ||
				name == "Drhtavica"||
				(name == "Temperatura" && value>38)
			) from $symptoms,		
            init(SalienceChecker $salCheck = new SalienceChecker(7);),
            action($salCheck.addSymptom();),
            result($salCheck)
		)
	then
		modify($mr) {setDisease($d);} 
		System.out.println("Groznica: satisfied " + $sdc.getSalience() + " out of " + $sdc.getMaxSymptomNum() + " symptoms.");
end


rule "Upala krajnika"
	no-loop
	lock-on-active true
	salience ($sdc.getSalience()/$sdc.getMaxSymptomNum()*100)
	when
		$d : Disease( name == "Upala krajnika" )
		$mr : Record (disease == null , $symptoms: symptoms)
		$sdc : SalienceChecker( $s : salience>=4 ) from accumulate(
			$symptom : Symptom (
				name == "Bol u grlu" ||
				name == "Glavobolja" ||
				name == "Drhtavica" ||
				(name == "Temperatura" && value>=40 && value<=41) ||
				name == "Bol koji se siri do usiju" ||
				name == "Gubitak apetita" ||
				name == "Umor" ||
				name == "Zuti sekret iz nosa"
				
			) from $symptoms,			
            init(SalienceChecker $salCheck = new SalienceChecker(8);),
            action($salCheck.addSymptom();),
            result($salCheck)
			
		)
	then
		modify($mr) {setDisease($d);} 
		System.out.println("Upala krajnika: satisfied " + $sdc.getSalience() + " out of " + $sdc.getMaxSymptomNum() + " symptoms.");
end



rule "Sinusna infekcija"
	no-loop
	lock-on-active true
	salience ($sdc.getSalience()/$sdc.getMaxSymptomNum()*100)
	when
		$dc : DateChecker() 
		$d : Disease( name == "Sinusna infekcija" )
		$patient : Patient(  $patientHistory : patientHistory )
		$mr : Record (disease == null , $symptoms: symptoms)
		
		
		//Pacijent treba da ima Groznicu ili Prehlad u poslednjih 60 dana
		Number(intValue >= 1) from accumulate (
			$oldDiag: Record( disease.name=="Prehlada" || disease.name=="Groznica", $dc.last60Days()<date ) from $patientHistory,
			init(int count = 0;),
			action(count++;),
			result(count)
		)
		
		$sdc : SalienceChecker( $s : salience>=3 ) from accumulate(
			$symptom : Symptom (
				name == "Oticanje oko ociju" ||
				name == "Glavobolja" ||
				name == "Zuti sekret iz nosa" ||
				name == "Bol u grlu" ||
				(name == "Temperatura" && value>38) ||
				name == "Kasalj"
				
			) from $symptoms,			
            init(SalienceChecker $salCheck = new SalienceChecker(6);),
            action($salCheck.addSymptom();),
            result($salCheck)
			
		)

	then
		modify($mr) {setDisease($d);}
		System.out.println("Sinusna infekcija: satisfied " + ($sdc.getSalience()+1) + " out of " + ($sdc.getMaxSymptomNum()+1) + " symptoms.");
end


/**DRUGA GRUPA**/

rule "Hipertenzija"
	no-loop
	lock-on-active
	salience 20
	when
		$dc : DateChecker() 
		$d: Disease(name == "Hipertenzija")
		$patient: Patient($patientHistory: patientHistory)
		$mr: Record($symptoms: symptoms, disease == null)
		
		// preuzmemo sve simptome iz bolesti u predhodnih 6 meseci
		$oldSymptoms: List() from accumulate(
			Record($dc.last6Months()<date, $currSymptoms: symptoms) from $patientHistory,
			init(List symptomList = new ArrayList<>();),
			action(symptomList.addAll($currSymptoms);)
			result(symptomList)
		)
		
		
		$symptomNum: Number(intValue >= 10) from accumulate(
			Symptom(name == "Visok krvni pritisak") from $oldSymptoms,
			init(int count = 0;),
			action(count++;),
			result(count)
		)
			
	then
		modify($mr) {setDisease($d);}
		System.out.println("Person suffering from hypertension! #: "+$symptomNum);
end




rule "Dijabetes"
    no-loop
	lock-on-active true
	salience 100
    when
        $d : Disease( name == "Dijabetes")
		$mr : Record (disease == null , $symptoms: symptoms)
		Number(intValue==4) from accumulate(
            $symptom: Symptom(
            	name == "Cesto uriniranje"	||
            	name == "Gubitak telesne tezine" 	||
            	name == "Zamor"	 ||
            	name == "Mucnina i povracanje" 	  		 		  		 	
            ) from $symptoms,
            init(int count = 0;),
            action(count++;),
            result(count)
        )
				
	then
		modify($mr) {setDisease($d);}
		System.out.println("Bolest Dijabetes.");

end

/**TRECA KATEGORIJA**/

rule "Hronicna bubrezna bolest"
	when
		$dc : DateChecker() 
		$d: Disease(name == "Hronicna bubrezna bolest")
		$hypertension: Disease(name == "Hipertenzija")
		$diabetes: Disease(name == "Dijabetes")
		$patient: Patient($patientHistory: patientHistory)
		$mr: Record($symptoms: symptoms, disease == null)
		
		// 2+ generalnih simptoma
		Number(intValue >= 2) from accumulate(
			Symptom(name == "Zamor"
				 || name == "Nocturia"
				 || name == "Otoci nogu i zglobova"
				 || name == "Gusenje"
				 || name == "Bol u grudima") from $symptoms,
			init(int count = 0;),
			action(count++;),
			result(count)
		
		)
		and
		(
			//boluje od hipertenzije vise od 6 mj
			Number(intValue >= 1) from accumulate(
				Record($dc.moreThan6Months()>date, disease == $hypertension) from $patientHistory,
				init(int count = 0;),
				action(count++;),
				result(count)		
			)
			or 
			// boluje od dijabetesa
			Number(intValue >= 1) from accumulate(
				Record(disease == $diabetes) from $patientHistory,
				init(int count = 0;),
				action(count++;),
				result(count)
			)
		)
		
	then
		modify($mr) {setDisease($d);}
		System.out.println("Hronicna bubrezna bolest");
end

rule "Akutna bubrezna povreda"
	when
		$dc : DateChecker() 
		$d: Disease(name == "Akutna bubrezna povreda")
		$patient : Patient(  $patientHistory : patientHistory )
		$mr:Record($symptoms: symptoms, disease == null)
			
		// 2+ generalnih simptoma
		Number(intValue >= 2) from accumulate(
			Symptom(name == "Zamor"
				 || name == "Gusenje"
				 || name == "Otoci nogu i zglobova"
				 || name == "Dijareja") from $symptoms,
			init(int count = 0;),
			action(count++;),
			result(count)
		)
		and 
		(
			// oporavak od peracjie
            ( 
                Number(intValue >= 1) from accumulate(
                			Symptom(name == "Oporavak od operacije") from $symptoms,
                			init(int count = 0;),
                			action(count++;),
                			reverse(count--;),
                			result(count)
                		)
            )            
            or (
            //zadnjih 14 dana dijagnostikovana bolest koja je kao simpom imala povisenu temp
            
            	//svi simptomi od zadnjih 14 dana
				$past14DaysSymptoms: List() from accumulate(
					Record($dc.last14Days()<date, $pasDis: disease != null) from $patientHistory,
					init(List pasSymps = new ArrayList<>(); ),
					action(pasSymps.addAll($pasDis.getSymptoms());),
					result(pasSymps)
				)
				and
				Number(intValue >= 1) from accumulate(
					Symptom(name == "Temperatura") from $past14DaysSymptoms,
					init(int count = 0;),
					action(count++;),
					result(count)
				)
            )		
            or
            //koristio antibiotike u zadnjih 21 dan
            (
            	// svi lekovi iz zadnih 21 dan
            	$past: List() from accumulate(
            		Record($dc.last21Days()<date, disease != null, $pastDrugs: medicine) from $patientHistory,
            		init(List pastDrugs = new ArrayList<>();),
            		action(pastDrugs.addAll($pastDrugs);),
            		result(pastDrugs)
            	)
            	and
            	Number(intValue >= 1) from accumulate(
            		Medicine(medicineType==MedicineType.ANTIBIOTIC) from $past,
            		init(int count = 0;),
            		action(count++;),
            		result(count)
            	)
            
            ) 
            
		)	
	
	then
		modify($mr) {setDisease($d);}
		System.out.println("Akutna bubrezna povreda");	
end






